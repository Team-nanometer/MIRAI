ファイル管理器とは
	単一種類

	-コンフィグ=コンフィグという設定ファイルを作ったもの
	-メインコンフィグ管理器=メインコンフィグ管理器というコンフィグチェッカーを作ったもの
	-メインコンフィグアプリ情報項目={「Version」=[アプリのバージョン],「InternalVersion」=[アプリの内部バージョン]}
	-ホットキー設定項目={ //ホットキー設定
		「KeySet_1.AssignmentKey.Any」=「Y」,
		「KeySet_1.AssignmentKey.Shift」=「×」,
		「KeySet_1.AssignmentKey.Ctrl」=「○」,
		「KeySet_1.AssignmentKey.Alt」=「○」,
		「KeySet_1.AssignmentKey.Win」=「×」,
		「KeySet_1.AssignmentTab.Index」=「1」,
		
		「KeySet_2.AssignmentKey.Any」=「U」,
		「KeySet_2.AssignmentKey.Shift」=「×」,
		「KeySet_2.AssignmentKey.Ctrl」=「○」,
		「KeySet_2.AssignmentKey.Alt」=「○」,
		「KeySet_2.AssignmentKey.Win」=「×」,
		「KeySet_2.AssignmentTab.Index」=「1」,
		
		「KeySet_3.AssignmentKey.Any」=「I」,
		「KeySet_3.AssignmentKey.Shift」=「×」,
		「KeySet_3.AssignmentKey.Ctrl」=「○」,
		「KeySet_3.AssignmentKey.Alt」=「○」,
		「KeySet_3.AssignmentKey.Win」=「×」,
		「KeySet_3.AssignmentTab.Index」=「1」,
		
		「KeySet_4.AssignmentKey.Any」=「O」,
		「KeySet_4.AssignmentKey.Shift」=「×」,
		「KeySet_4.AssignmentKey.Ctrl」=「○」,
		「KeySet_4.AssignmentKey.Alt」=「○」,
		「KeySet_4.AssignmentKey.Win」=「×」,
		「KeySet_4.AssignmentTab.Index」=「1」,
		
		「KeySet_5.AssignmentKey.Any」=「P」,
		「KeySet_5.AssignmentKey.Shift」=「×」,
		「KeySet_5.AssignmentKey.Ctrl」=「○」,
		「KeySet_5.AssignmentKey.Alt」=「○」,
		「KeySet_5.AssignmentKey.Win」=「×」,
		「KeySet_5.AssignmentTab.Index」=「1」
	}
	-ファイル名生成設定項目={ //ファイル名生成関連設定
		「Preset.Format.Text」=「:1:-:2:_:年::月::日::拡張子:」,
		「Preset.Format.WithoutSpace」=「○」,
		「Preset.Genre.Name」=「ゲーム」,
		「Preset.Genre.Index」=「1」,
		「Preset.Type_1.Name」=「Apex Legends」,
		「Preset.Type_1.Index」=「1」,
		「Preset.Type_2.Name」=「Bloodhound」,
		「Preset.Type_2.Index」=「2」,
		「Preset.Custom_1.Text」=「」,
		「Preset.Custom_2.Text」=「」,
		「Preset.FilenameExtension.Name」=「.ai」,
		「Preset.FilenameExtension.Index」=「2」
	}
	-サウンドデバイスマネージャー設定項目={ //サウンドデバイスマネージャー設定
		「RenderDevice.DefaultDevice.Index」=「1」,
		「RenderDevice.DefaultCommunicationDevice.Index」=「1」,
		「CaptureDevice.DefaultDevice.Index」=「1」,
		「CaptureDevice.DefaultCommunicationDevice.Index」=「1」
	}

	+ファイル名生成設定:辞書
	+サウンドデバイスマネージャー設定:辞書
	+ホットキー設定:辞書

	自分でファイルチェックする手順 //フォルダー&コンフィグ&プリセットのチェック
		メインコンフィグ管理器の設定ファイル名=コンフィグ
			
		////フォルダーチェック////
			もしアプリのコンフィグフォルダーが存在しないならアプリのコンフィグフォルダーを作成する	//ドキュメントフォルダーへアプリ名のフォルダーが存在しない場合、フォルダーを作成する
			もしアプリのプリセットフォルダーが存在しないならアプリのプリセットフォルダーを作成する	//ドキュメントフォルダーへプリセットフォルダーが存在しない場合、フォルダーを作成する
			もしアプリのアップデートフォルダーが存在しないならアプリのアップデートフォルダーを作成する//ドキュメントフォルダーへアップデートフォルダーが存在しない場合、フォルダーを作成する
			もしアプリのリソースフォルダーが存在しないならアプリのリソースフォルダーを作成する//アプリの位置へリソースフォルダーが存在しない場合、フォルダーを作成する
			もし「[アプリのリソースフォルダー]Modules」というフォルダが存在しないなら「[アプリのリソースフォルダー]」へ「Modules」というフォルダを作成する //リソースフォルダーへモジュールフォルダーを作成
			もし「[アプリのリソースフォルダー]Modules\NirCmd」というフォルダが存在しないなら「[アプリのリソースフォルダー]\Modules」へ「NirCmd」というフォルダを作成する //モジュールフォルダーへNirCmdフォルダーを作成
		////フォルダーチェック////

		////コンフィグの設定&読み込み////
			コンフィグのファイル名=「[アプリのコンフィグフォルダーの絶対パス]Config.ini」
			もし[コンフィグのファイル名]が存在するなら //既にコンフィグファイルが存在する場合、バージョンをチェックして220204以下であれば新たにコンフィグを生成して古いコンフィグはファイル名を変更する
				もし[コンフィグについて「App」から「InternalVersion」を取得したもの]<220205なら
					「[アプリのコンフィグフォルダー]Config.ini」を「Config_Old.ini」へ変更する
					「コンフィグファイルが現在のバージョンに対応していない形式だったため、新しく生成します。[改行]以前のコンフィグファイルは以下の場所に保存されています。[改行]"[アプリのコンフィグフォルダー]Config_Old_[コンフィグについて「App」から「InternalVersion」を取得したもの].ini"」を警告アイコンで表示する
				もし終わり
			もし終わり
			
			メインコンフィグ管理器で「App」をメインコンフィグアプリ情報項目としてコンフィグチェックする //アプリ情報をコンフィグへ書き込み
			メインコンフィグ管理器で「Hotkey」をホットキー設定項目としてコンフィグチェックする //初期設定項目を書き込み (既に設定されている項目は上書きしない)
			メインコンフィグ管理器で「FilenameGenerator」をファイル名生成設定項目としてコンフィグチェックする //初期設定項目を書き込み (既に設定されている項目は上書きしない)
			メインコンフィグ管理器で「SoundDeviceManager」をサウンドデバイスマネージャー設定項目としてコンフィグチェックする //初期設定項目を書き込み (既に設定されている項目は上書きしない)
			
			ホットキー設定=コンフィグについて「Hotkey」からすべて取得したもの //コンフィグから設定項目を読み込み
			ファイル名生成設定=コンフィグについて「FilenameGenerator」からすべて取得したもの //コンフィグから設定項目を読み込み
			サウンドデバイスマネージャー設定=コンフィグについて「SoundDeviceManager」からすべて取得したもの //コンフィグから設定項目を読み込み
		////コンフィグの設定&読み込み////

		////プリセットの読み込み////
			//元作品タイトルプリセットのファイル情報を設定
			元作品タイトルプリセットのプリセットファイル=プリセットファイルというファイル情報(「[アプリのプリセットフォルダー]MIRAIFilenameGeneratorPreset_[ファイル管理器のファイル名生成設定から「Preset.Genre.Name」を得たもの].json」)を作ったもの
			//拡張子プリセットのファイル情報を設定
			拡張子プリセットのプリセットファイル=プリセットファイルというファイル情報(「[アプリのプリセットフォルダー]MIRAIFilenameExtensionPreset.json」)を作ったもの
			
			//プリセットファイルが存在しない場合、初期値から作成する
				//元作品タイトルプリセットファイル(ゲーム)
				もし元作品タイトルプリセットのプリセットファイルが存在しないなら
					元作品タイトルプリセットの初期プリセットをJSON形式として整形して書き出したものを元作品タイトルプリセットのプリセットファイルの絶対パスへ保存する
				もし終わり
				//拡張子プリセットファイル
				もし拡張子プリセットのプリセットファイルが存在しないなら
					拡張子プリセットの初期プリセットをJSON形式として整形して書き出したものを拡張子プリセットのプリセットファイルの絶対パスへ保存する
				もし終わり
			
			//プリセットファイルを読み込み(設定項目=プリセット)
				//元作品タイトルプリセット
				元作品タイトルプリセットのプリセット=[元作品タイトルプリセットのプリセットファイルから読み込んだもの]をJSON形式として読み取ったもの
				//拡張子プリセット
				拡張子プリセットのプリセット=[拡張子プリセットのプリセットファイルから読み込んだもの]をJSON形式として読み取ったもの
			
			//元作品タイトルプリセットの一覧をフォルダーから取得
			元作品タイトルプリセットのプリセットファイル一覧={}
			アプリのプリセットフォルダーのファイル一覧をファイルへそれぞれ繰り返す
				もしファイルの名前が「MIRAIFilenameGeneratorPreset_」で始まるかつファイルの名前の拡張子だけが「json」なら
					元作品タイトルプリセットのプリセットファイル一覧へファイルの絶対パスを追加する
				もし終わり
			繰り返し終わり
			元作品タイトルプリセットのプリセットファイル一覧を名前順に並び替える
		////プリセットの読み込み////
	終わり

	自分でコンフィグ保存手順
		ファイル管理器のホットキー設定の見出しを設定内容へそれぞれ繰り返す //ホットキー設定
			コンフィグについて「Hotkey」で設定内容へファイル管理器のホットキー設定(設定内容)を設定する
		繰り返し終わり
		
		ファイル管理器のファイル名生成設定の見出しを設定内容へそれぞれ繰り返す //ファイル名生成設定
			コンフィグについて「FilenameGenerator」で設定内容へファイル管理器のファイル名生成設定(設定内容)を設定する
		繰り返し終わり
		
		ファイル管理器のサウンドデバイスマネージャー設定の見出しを設定内容へそれぞれ繰り返す //サウンドデバイスマネージャー設定
			コンフィグについて「SoundDeviceManager」で設定内容へファイル管理器のサウンドデバイスマネージャー設定(設定内容)を設定する
		繰り返し終わり
	終わり
終わり